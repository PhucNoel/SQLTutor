erEntityOrRelationship(?name) :- erEntity(?name).
erEntityOrRelationship(?name) :- erRelationship(?name).

// the table named ?tname references the entity ?ent
erTableRefsEntity(?tname,?ent) :- 
  sqlTableName(?tref,?tname),
  erEntity(?ent),
  erAttributeMapsTo(?ent,?attr,?tname,?column).

erFKJoin(?rel, ?pktable, ?pkattr, ?fktable, ?fkattr) :-
  erRelationshipJoinType(?rel, 'foreign_key'),
  erJoinPK(?rel, 0, ?pktable, ?pkattr),
  erJoinFK(?rel, 0, ?fktable, ?fkattr).

// ?pos is the edge index of the primary key for relationship ?rel 
erFKJoinPKSide(?rel,?pos) :-
  erRelationshipJoinType(?rel, 'foreign_key'),
  erRelationshipEdgeCardinality(?rel,?pos,?card),
  ?card >= 0.
  
// ?pos is the edge index of the foreign key for relationship ?rel
erFKJoinFKSide(?rel,?pos) :-
  erRelationshipJoinType(?rel, 'foreign_key'),
  erRelationshipEdgeCardinality(?rel,?pos,?card),
  ?card < 0.

// ?pkSide and ?fkSide are the positions of the primary and 
// foreign key edges of relationship ?rel 
erFKJoinSides(?rel,?pkSide,?fkSide) :- 
  erFKJoinPKSide(?rel,?pkSide),
  erFKJoinFKSide(?rel,?fkSide).
  

erLookupJoinKeyPair(?rel, ?pos, ?pktable, ?pkattr, ?fktable, ?fkattr) :-
  erRelationshipJoinType(?rel, 'lookup_table'),
  erJoinPK(?rel, ?pos, ?pktable, ?pkattr),
  erJoinFK(?rel, ?pos, ?fktable, ?fkattr).
