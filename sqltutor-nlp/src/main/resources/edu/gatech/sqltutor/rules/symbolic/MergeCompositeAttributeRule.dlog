
// ?attrList has composite attribute ?entity.?composite child ?childAttr in token ?attrToken
ruleMergeCompositeAttribute_compositeChild(?attrList, ?entity, ?composite, ?attrToken, ?childAttr) :-
  // attribute list with attribute token child
  symType(?attrList, 'ATTRIBUTE_LIST'),
  symParentOf(?attrList, ?attrToken, ?attrPos),
  symType(?attrToken, 'ATTRIBUTE'),
  // where attribute is child of composite attribute
  symRefsAttribute(?attrToken, ?entity, ?childAttr),
  erAttributeParent(?entity, ?composite, ?childAttr),
  erAttributeIsComposite(?entity, ?composite).

// ?attrList has at least one child of composite attribute ?entity.?composite  
ruleMergeCompositeAttribute_hasCompositeChild(?attrList, ?entity, ?composite) :-
  ruleMergeCompositeAttribute_compositeChild(?attrList, ?entity, ?composite, ?attrToken, ?childAttr).
  
// ?attrList has at least one, but not all of, the child attribute of composite attribute ?entity.?composite
ruleMergeCompositeAttribute_hasIncompleteComposite(?attrList, ?entity, ?composite) :-
  ruleMergeCompositeAttribute_hasCompositeChild(?attrList, ?entity, ?composite),
  erAttributeParent(?entity, ?composite, ?childAttr),
  not ruleMergeCompositeAttribute_compositeChild(?attrList, ?entity, ?composite, ?childToken, ?childAttr).
  
// ?attrList has an attribute token for every child attribute of composite attribute ?entity.?composite
ruleMergeCompositeAttribute_hasAllCompositeChildren(?attrList, ?entity, ?composite) :-
  ruleMergeCompositeAttribute_hasCompositeChild(?attrList, ?entity, ?composite),
  not ruleMergeCompositeAttribute_hasIncompleteComposite(?attrList, ?entity, ?composite).
